"TonUtil.fif" include
"Asm.fif" include
"compiled-smc.fif" include
"common.fif" include

variable owner_public_key
variable owner_private_key

"owner_key.pk" file>B owner_private_key !
owner_private_key @ priv>pub owner_public_key !

"EQBNHgU3GiNnGewebGogIfblJhInOtKkbO6knXDXQ24BBOJX" constant owner_address
owner_address $>smca // x y z -1
2drop

2constant owner_raw

<b 
    1 32 u,
    owner_public_key @ B,
    owner_raw Addr,
b> storage !

variable message_to_sign
variable res_message
variable signature
variable exit_code

<b 
    1 32 u,
    2 32 u, 
    50000 Gram,
b> message_to_sign !

message_to_sign @ // c
hashu // x
owner_private_key @ // B'
ed25519_sign_uint // B'' (signature)
signature !

<b 
    signature @ B,
    1 32 u,
    2 32 u,
    50000 Gram,
b> <s res_message !

res_message @
recv_external
code <s
storage @ 
c7
runvmctx  // exit_code c4'

storage !
exit_code !

."Exit code " exit_code @ . cr
exit_code @ 0 - abort"Test failed!"